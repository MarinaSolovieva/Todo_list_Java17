plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
 }

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    validationApiVersion = '2.0.1.Final'
    hibernateValidatorVersion = '7.0.0.Final'
    mapStructVersion = '1.4.2.Final'
    mapStructProcessorVersion = '1.4.2.Final'
    apacheCommonsLang3Version = '3.0'
    apacheCommonsCollections4Version = '4.1'
    checkstyleVersion = '9.1'
    springCloudVersion = '3.0.3'
    embeddedPostgresqlVersion = '2.0.10'
}

apply plugin: 'checkstyle'
checkstyle {
    toolVersion '9.1'
    configFile file("config/checkstyle/checkstyle.xml")
    ignoreFailures = false
}
checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}

def testExcludeList = [
        '**/model/**',
        '**/exception_handling/exception/**',
        '**/exception_handling/UserIncorrectData.class',
        '**/dao/**',
        '**/mapper/**'
]

jacoco {
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/jacocoHtml')
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: testExcludeList)
        }))
    }
    dependsOn test
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: testExcludeList)
        }))
    }
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.liquibase:liquibase-core'

    runtimeOnly 'org.postgresql:postgresql'

    compileOnly "javax.validation:validation-api:$validationApiVersion"
    compileOnly "org.hibernate:hibernate-validator:$hibernateValidatorVersion"

    implementation "org.mapstruct:mapstruct:$mapStructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructProcessorVersion"

    implementation "org.apache.commons:commons-lang3:$apacheCommonsLang3Version"
    implementation "org.apache.commons:commons-collections4:$apacheCommonsCollections4Version"

    implementation "com.puppycrawl.tools:checkstyle:$checkstyleVersion"

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.springframework.cloud:spring-cloud-starter-bootstrap:$springCloudVersion"
    testImplementation "com.playtika.testcontainers:embedded-postgresql:$embeddedPostgresqlVersion"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}


